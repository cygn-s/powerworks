buildscript {
    ext.kotlin_version = '1.3.71'
    ext.lwjglVersion = "3.0.0a"
    ext.gdxVersion = '1.9.8'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'edu.sc.seis.gradle:launch4j:2.4.6'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.71"
}

version '0.4.1'

subprojects {

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "http://clojars.org/repo/" }
    }

    apply plugin: "application"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: 'edu.sc.seis.launch4j'

    dependencies {
        compile "com.esotericsoftware:kryo:5.0.0-RC4"
        compile "com.esotericsoftware:kryonet:2.22.0-RC1"
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile 'com.github.philfrei:AudioCue:-SNAPSHOT'
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

project(":client") {

    dependencies {
        compile project(":shared")
    }

    evaluationDependsOn(":shared")

    mainClassName = "main.GameKt"

    launch4j {
        mainClassName = "main.GameKt"
    }

    jar {
        manifest {
            attributes 'Main-Class': 'main.GameKt'
        }

        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    
    sourceSets {
        main {
            kotlin.srcDirs += project(":shared").sourceSets.main.kotlin.srcDirs
            resources.srcDirs += project(":shared").sourceSets.main.resources.srcDirs
        }
    }
}

project(":server") {

    dependencies {
        compile project(":shared")
    }

    evaluationDependsOn(":shared")

    mainClassName = "network.ServerKt"

    launch4j {
        mainClassName = "network.ServerKt"
    }

    sourceSets {
        main {
            kotlin.srcDirs += project(":shared").sourceSets.main.kotlin.srcDirs
            resources.srcDirs += project(":shared").sourceSets.main.resources.srcDirs
        }
    }
}